///////////////////////////////////////////////////////////////////////
//  Тестовые таблицы
///////////////////////////////////////////////////////////////////////
Таблица = "
|{
|	'Колонки': {'Объект': 'СТРОКА(50)', 'Статья': 'СТРОКА(50)',	'Сумма': 'ЧИСЛО(15, 2)'},
|	'Строки': [
|		['Об1', 'Ст1', 1],
|		['Об1', 'Ст2', 1],
|		['Об2', 'Ст1', 1],
|	]
|}";
Отношение = "
|{
|	'Колонки': {'ОбъектТаблица': 'СТРОКА(50)', 'СтатьяТаблица': 'СТРОКА(50)', 'ОбъектБаза': 'СТРОКА(50)', 'СтатьяБаза': 'СТРОКА(50)'},
|	'Строки': [
|		['Об1', 'Ст1', 'Об1', 'Ст1'],
|		['Об1', 'Ст2', 'Об1', 'Ст1'],
|		['Об2', 'Ст1', 'Об2', 'Ст1'],
|	]
|}";
База = "
|{
|	'Колонки': {'Объект': 'СТРОКА(50)',	'Статья': 'СТРОКА(50)',	'Очередь': 'ЧИСЛО(2)', 'Сумма': 'ЧИСЛО(15, 2)'},
|	'Строки': [
|		['Об1', 'Ст1', 1, 1],
|		['Об1', 'Ст1', 2, 1],
|		['Об1', 'Ст1', 1, 2],
|		['Об1', 'Ст1', 2, 1],
|		['Об2', 'Ст1', 1, 11],
|		['Об2', 'Ст1', 2, 10],
|	]
|}";
ОтношениеЛимитовКСублимитам = "
|{
|	'Колонки': {'Очередь': 'ЧИСЛО(2)', 'Сублимит': 'СТРОКА(50)'},
|	'Строки': [
|		['1', 'С1'],
|		['2', 'С2'],
|	]
|}";
Сублимиты = "
|{
|	'Колонки': {'Сублимит': 'СТРОКА(50)',	'Сумма': 'ЧИСЛО(15, 2)'},
|	'Строки': [
|		['С1', 1],
|		['С2', 1],
|	]
|}";
///////////////////////////////////////////////////////////////////////
//  Модель DSL
///////////////////////////////////////////////////////////////////////
МодельРаспределения = Общий.МодельРаспределения()
	.Таблица(Таблица)
		.Измерения()
			.Поле("Объект", "ОбъектТаблица")
			.Поле("Статья", "СтатьяТаблица")
		.Ресурсы()
			.Поле("Сумма")
	.Отношение(Отношение)
		.ИзмеренияТаблицы()
			.Поле("ОбъектТаблица")
			.Поле("СтатьяТаблица")
		.ИзмеренияБазы()
			.Поле("ОбъектБаза")
			.Поле("СтатьяБаза")
	.База(База)
		.Измерения()
			.Поле("Объект", "ОбъектБаза")
			.Поле("Статья", "СтатьяБаза")
		.Ресурсы()
			.Поле("*")
		.Реквизиты()
			.Поле("Очередь")
		.Порядок("Очередь")
;
//  Распределение с ограничением
//  Инициализация таблиц: отношения лимитов к сублимитам, сублимитов
//  Заготовка отборов: для отношения, для сублимитов
ОтборОтношенияКСублимитам = Новый Структура("Очередь");
ОтборСублимитов = Новый Структура("Сублимит");
Сублимиты = Общий.СтруктураВТаблицуЗначений(Сублимиты);
ОтношениеЛимитовКСублимитам = Общий.СтруктураВТаблицуЗначений(ОтношениеЛимитовКСублимитам);
Схема = МодельРаспределения.Схема;
Если Схема.Таблица.Ресурсы.Количество() > 1 Тогда
	Ресурсы = Схема.Таблица.Ресурсы;				
Иначе
	Ресурсы = Схема.База.Ресурсы;
КонецЕсли;
Пока МодельРаспределения.Следующий() Цикл
	Если РезультатРаспределения = Неопределено Тогда
		РезультатРаспределения = МодельРаспределения.РезультатРаспределения.СкопироватьКолонки();
		РезультатРаспределения.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(9));
		Для Каждого Ресурс Из Ресурсы Цикл
			ТипЧисло = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
			РезультатРаспределения.Колонки.Добавить(Ресурс.Псевдоним + "НачальныйОстаток", ТипЧисло);
			РезультатРаспределения.Колонки.Добавить(Ресурс.Псевдоним + "КонечныйОстаток", ТипЧисло);
			РезультатРаспределения.Колонки.Добавить(Ресурс.Псевдоним + "Распределение", ТипЧисло);				
		КонецЦикла;
	КонецЕсли;
	СтрокаРезультата = МодельРаспределения.СтрокаРезультата;
	СтрокаТаблицы = МодельРаспределения.ИтераторТаблицы.ТекущиеДанные;
	СтрокаРаспределения = РезультатРаспределения.Добавить();
	//  Определение суммы распределения с учетом дополнительного ограничения
	ЗаполнитьЗначенияСвойств(ОтборОтношенияКСублимитам, СтрокаРезультата);
	СтрокиОтношения = ОтношениеЛимитовКСублимитам.НайтиСтроки(ОтборОтношенияКСублимитам);
	СписокСублимитов = Новый Массив;
	Для Каждого СтрокаОтношения Из СтрокиОтношения Цикл
		ЗаполнитьЗначенияСвойств(ОтборСублимитов, СтрокаОтношения);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокСублимитов, Сублимиты.НайтиСтроки(ОтборСублимитов), Истина);
	КонецЦикла;
	Если ЗначениеЗаполнено(СписокСублимитов) Тогда
		СуммаРаспределения = СтрокаРезультата.Сумма;
		Для Каждого СтрокаСублимита Из СписокСублимитов Цикл
			СуммаРаспределения = Мин(СуммаРаспределения, СтрокаСублимита.Сумма);
		КонецЦикла;
	Иначе
		СуммаРаспределения = 0;
	КонецЕсли;
	СтрокаРезультата.Сумма = СуммаРаспределения;//  закомментируйте, чтобы снять ограничение
	Для Каждого СтрокаСублимита Из СписокСублимитов Цикл
		СтрокаСублимита.Сумма = СтрокаСублимита.Сумма - СуммаРаспределения;
	КонецЦикла;
	//  Фиксация результата распределения
	ЗаполнитьЗначенияСвойств(СтрокаРаспределения, СтрокаРезультата);
	Для Каждого Ресурс Из Ресурсы Цикл
		ИмяПоля = Ресурс.Псевдоним;
		СтрокаРаспределения[ИмяПоля + "НачальныйОстаток"] = СтрокаТаблицы[ИмяПоля];
		СтрокаРаспределения[ИмяПоля + "Распределение"] = СтрокаРезультата[ИмяПоля];
		СтрокаРаспределения[ИмяПоля + "КонечныйОстаток"] = СтрокаТаблицы[ИмяПоля] - СтрокаРезультата[ИмяПоля];
	КонецЦикла;			
КонецЦикла;
Если РезультатРаспределения = Неопределено Тогда
	ВызватьИсключение "Таблица не распределилась!";
КонецЕсли;
